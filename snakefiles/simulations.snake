import numpy as np
import msprime
from src.twosfs import sims2sfs

def parameter_map(prefix):
    if prefix == 'test':
        return {'sample_size': 4, 'num_replicates': 100}
    elif prefix == 'kingman':
        return {}
    elif prefix.startswith('xibeta'):
        alpha = float(prefix.split('alpha=')[1])
        return {'model': msprime.BetaCoalescent(alpha=alpha)}
    else:
        raise KeyError('Simulation prefix not found')

ALPHAS = [f'{a:.2f}' for a in np.arange(1.5, 2.0, 0.1)]

rule simulations:
    input:
        'simulations/msprime/kingman.npz',
        expand('simulations/msprime/xibeta-alpha={alpha}.npz', alpha=ALPHAS)
    output:
        'simulations.tgz'
    shell:
        'tar -czf {output} {input}'

rule msprime:
    output:
        temp('simulations/.tmp/{prefix}.rep{rep}.npz')
    run:
        default_parameters = {
            'sample_size': 100,
            'length': 100,
            'recombination_rate': 0.1,
            'random_seed': 1 + int(wildcards.rep),
            'num_replicates': 10000,
        }
        parameters = dict(default_parameters, **parameter_map(wildcards.prefix))
        sims = msprime.simulate(**parameters)
        onesfs, twosfs = sims2sfs(sims,
                                  parameters['sample_size'],
                                  parameters['length'])
        np.savez_compressed(output[0], onesfs=onesfs, twosfs=twosfs)

rule average_runs:
    output:
        'simulations/msprime/{prefix}.npz'
    input:
        expand('simulations/.tmp/{prefix}.rep{rep}.npz', rep=range(10), allow_missing=True)
    run:
        onesfs = 0.0
        twosfs = 0.0
        for infn in input:
            data = np.load(infn)
            onesfs += data['onesfs']
            twosfs += data['twosfs']
        onesfs /= len(input)
        twosfs /= len(input)
        np.savez_compressed(output[0], onesfs=onesfs, twosfs=twosfs)
