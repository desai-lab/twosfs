import numpy as np
import msprime
from src.twosfs import sims2sfs

def parameter_map(prefix):
    """Convert a file prefix to a set of msprime parameters."""
    default_parameters = {
        'sample_size': 100,
        'length': 100,
        'recombination_rate': 0.1,
        'num_replicates': 10000,
    }

    if prefix == 'test':
        parameters = {'sample_size': 11, 'num_replicates': 100}
    elif prefix == 'kingman':
        parameters = {}
    elif prefix.startswith('xibeta'):
        alpha = float(prefix.split('alpha=')[1])
        parameters = {'model': msprime.BetaCoalescent(alpha=alpha)}
    else:
        raise KeyError('Simulation prefix not found')

    return dict(default_parameters, **parameters)

ALPHAS = [f'{a:.2f}' for a in np.arange(1.5, 2.0, 0.05)]

rule simulations:
    group: 'cleanup'
    input:
        'simulations/msprime/kingman.npz',
        expand('simulations/msprime/xibeta-alpha={alpha}.npz', alpha=ALPHAS)
    output:
        'simulations.tgz'
    shell:
        'tar -czf {output} {input}'

rule msprime:
    output:
        temp('simulations/.tmp/{prefix}.rep{rep}.npz')
    run:
        parameters = parameter_map(wildcards.prefix)
        parameters['random_seed'] = 1 + int(wildcards.rep)
        sims = msprime.simulate(**parameters)
        onesfs, twosfs = sims2sfs(sims,
                                  parameters['sample_size'],
                                  parameters['length'])
        np.savez_compressed(output[0], onesfs=onesfs, twosfs=twosfs)

rule average_runs:
    group: 'cleanup'
    output:
        'simulations/msprime/{prefix}.npz'
    input:
        expand('simulations/.tmp/{prefix}.rep{rep}.npz', rep=range(10), allow_missing=True)
    run:
        onesfs = 0.0
        twosfs = 0.0
        for infn in input:
            data = np.load(infn)
            onesfs += data['onesfs']
            twosfs += data['twosfs']
        onesfs /= len(input)
        twosfs /= len(input)
        np.savez_compressed(output[0], onesfs=onesfs, twosfs=twosfs)
