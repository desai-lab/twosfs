import numpy as np
import msprime
import random
import json
import os
import h5py

from twosfs.config import configuration_from_json, parse_parameter_string
from slim.sfs_slim import spectra_from_tree_file
from twosfs.simulations import filename2seed
from twosfs.statistics import max_ks_distance

config = configuration_from_json("simulation_parameters.json")

rule search_forward_recombination_all:
    input:
        list(config.forward_recombination_search_files()),
    resources:
        mem=100,
        time=5,

rule forward_ks_distances_all:
    input:
        list(config.forward_ks_distance_files())
    resources:
        mem=100,
        time=5,

rule fitted_forward_demographies_all:
    input:
        list(config.fitted_forward_demography_files())
    resources:
        mem=100,
        time=5,

rule simulate_tree_file:
    output:
        config.tree_file,
    wildcard_constraints:
        rep="\d+",
    resources:
        mem=5000,
        time=800
    run:
        params=parse_parameter_string(wildcards.params)
        params["genome_length"] = config.slim_parameters["genome_length"]
        params["pop_size"] = config.slim_parameters["pop_size"]
        os.system( "slim -d \"fname='{}'\" -d mut_rate={} -d s={} -d rec_rate={} -d N={} -d L={} -s {} slim/run_slim_simulation.txt".format(output[0].replace('"','\\\"'), params["mut_rate"], params["s"], params["rec_rate"], int(params["pop_size"]), int(params["genome_length"]), filename2seed(output[0])) )


ruleorder: spectra_from_SLiM > simulate_initial_spectra

rule spectra_from_SLiM:
    input:
        config.tree_file,
    output:
        temp(config.initial_spectra_file),
    wildcard_constraints:
        model="sel",
        rep="\d+",
    resources:
        mem=3000,
        time=10,
    run:
        random.seed(filename2seed(output[0]))
        spectra_params = config.slim_parameters
        spectra_params["recombination_rate"] = parse_parameter_string(wildcards.params)["rec_rate"]
        samples = random.sample( 
            range(2*int(config.slim_parameters["pop_size"])), 
            k=config.slim_parameters["num_samples"]
        )
        spectra_params["samples"] = samples
        spectra = spectra_from_tree_file(input[0], spectra_params)
        spectra.save(output[0])


ruleorder: add_runs_from_SLiM > add_runs

rule add_runs_from_SLiM:
    output:
        "simulations/initial_spectra/model=sel.{params}.rep=all.hdf5",
    input:
        expand(
            "simulations/initial_spectra/model=sel.{params}.rep={rep}.hdf5",
            rep=range(config.slim_parameters["nruns"]),
            allow_missing=True,
        ),
    resources:
        time=10,
        mem=1000,
    run:
        total = add_spectra(load_spectra(infn) for infn in input)
        total.save(output[0])


ruleorder: search_recombination_rate_from_slim > search_recombination_rate

rule search_recombination_rate_from_slim:
    input:
        spectra_file=config.initial_spectra_file.replace(".rep={rep}.", ".rep=all."),
        demo_file=config.fitted_demography_file,
    output:
        config.recombination_search_file,
    wildcard_constraints:
        model = "sel"
    resources:
        time=120,
        mem=5000,
    run:
        params = parse_parameter_string(wildcards.params)
        rng = np.random.default_rng(filename2seed(output[0]))
        raw_spectra = load_spectra(input.spectra_file)
        with open(input.demo_file) as f:
            model_parameters = json.load(f)
        num_pairs = np.zeros(len(raw_spectra.windows)-1)
        num_pairs[0:int(wildcards.sequence_length)] = int(wildcards.pair_density)
        spectra_samp = sample_spectra(raw_spectra, num_pairs=num_pairs, rng=rng)
        sim_kwargs = dict(
            model="pwc",
            model_parameters=model_parameters,
            msprime_parameters=(
                config.msprime_parameters
                | {
                    "sequence_length": int(wildcards.sequence_length),
                    "num_replicates": config.search_num_replicates,
                }
            ),
            random_seed=rng,
        )
        distances = (np.arange(int(wildcards.sequence_length) // 3) + 1) * 3
        search_recombination_rates_save(
            output[0],
            spectra_samp,
            config.k_max,
            distances,
            wildcards.folded == "True",
            sim_kwargs,
            config.slim_parameters["search_r_low"] * spectra_samp.scaled_recombination_rate(),
            config.slim_parameters["search_r_high"] * spectra_samp.scaled_recombination_rate(),
            config.search_iters
        )


