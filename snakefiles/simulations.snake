import numpy as np
import json
import gzip

from twosfs.config import configuration_from_json, parse_parameter_string
from twosfs.simulations import filename2seed, simulate_spectra
from twosfs.spectra import add_spectra, load_spectra
from twosfs.statistics import threefold_degen_sites, sample_ks_statistics

config = configuration_from_json("simulation_parameters.json")


rule simulate_initial_spectra_all:
    input:
        list(config.initial_spectra_files()),


rule fit_demographies_all:
    input:
        list(config.fitted_demography_files()),


rule simulate_fitted_spectra_all:
    input:
        list(config.fitted_spectra_files()),


rule compute_power_all:
    input:
        list(config.ks_distance_files()),


rule simulate_initial_spectra:
    output:
        temp(config.initial_spectra_file),
    wildcard_constraints:
        model=not "sel",
        rep="\d+",
    run:
        spectra = simulate_spectra(
            model=wildcards.model,
            model_parameters=parse_parameter_string(wildcards.params),
            msprime_parameters=config.msprime_parameters,
            scaled_recombination_rate=config.scaled_recombination_rate,
            random_seed=filename2seed(output[0]),
        )
        spectra.save(output[0])


rule fit_demographies:
    input:
        config.initial_spectra_file.replace(".rep={rep}.", ".rep=all."),
    output:
        config.fitted_demography_file,
    resources:
        time=10,
        mem=1000,
    run:
        spectra = load_spectra(input[0])
        fit = spectra.fit_pwc_demography(
            folded=wildcards.folded == "True",
            k_max=config.k_max,
            num_epochs=config.num_epochs,
            penalty_coef=config.penalty_coef,
        )
        with open(output[0], "w") as f:
            f.write(fit.toJson())


rule simulate_fitted_spectra:
    output:
        temp(config.fitted_spectra_file),
    input:
        config.fitted_demography_file,
    wildcard_constraints:
        rep="\d+",
    resources:
        time=60,
        mem=1000,
    run:
        with open(input[0], "r") as f:
            model_parameters = json.load(f)
        r = config.scaled_recombination_rate * float(wildcards.rec_factor)
        spectra = simulate_spectra(
            model="pwc",
            model_parameters=model_parameters,
            msprime_parameters=config.msprime_parameters,
            scaled_recombination_rate=r,
            random_seed=filename2seed(output[0]),
        )
        spectra.save(output[0])


rule add_runs:
    output:
        "{prefix}.rep=all.npz",
    input:
        expand(
            "{prefix}.rep={rep}.npz",
            rep=range(config.nruns),
            allow_missing=True,
        ),
    resources:
        time=60,
        mem=1000,
    run:
        total = add_spectra(load_spectra(infn) for infn in input)
        total.save(output[0])


rule compute_power:
    output:
        config.ks_distance_file,
    input:
        initial=config.initial_spectra_file.replace(".rep={rep}.", ".rep=all."),
        fitted_folded=config.fitted_spectra_file.replace(
            ".folded={folded}.rec_factor={rec_factor}.rep={rep}.",
            ".folded=True.rec_factor=1.0.rep=all.",
        ),
        fitted_unfolded=config.fitted_spectra_file.replace(
            ".folded={folded}.rec_factor={rec_factor}.rep={rep}.",
            ".folded=False.rec_factor=1.0.rep=all.",
        ),
    run:
        s_initial = load_spectra(input.initial)
        s_fitted_folded = load_spectra(input.fitted_folded)
        s_fitted_unfolded = load_spectra(input.fitted_unfolded)
        data = []
        for pd in config.pair_densities:
            for md in config.max_distances:
                num_pairs = pd * threefold_degen_sites(s_initial.twosfs.shape[0], md)
                ks_folded = sample_ks_statistics(
                    s_initial,
                    s_fitted_folded,
                    config.k_max,
                    True,
                    config.n_reps,
                    num_pairs,
                )
                ks_unfolded = sample_ks_statistics(
                    s_initial,
                    s_fitted_unfolded,
                    config.k_max,
                    False,
                    config.n_reps,
                    num_pairs,
                )
                data.append(
                    {
                        "pair_density": pd,
                        "max_d": md,
                        "folded": True,
                        "ks_stats": list(ks_folded),
                    }
                )
                data.append(
                    {
                        "pair_density": pd,
                        "max_d": md,
                        "folded": False,
                        "ks_stats": list(ks_unfolded),
                    }
                )
        with gzip.open(output[0], "wt") as outfile:
            outfile.write(json.dumps(data))
